%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% --------------- EXERCICE 3 - Résolveur de Sudoku --------------- %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Création de la grille de jeu
valeur(1..9).
pos(0..8).

% Génération de toutes les grilles possibles, 1 chiffre par case
1 { case(X,Y,Z) : valeur(Z) } 1 :- pos(X), pos(Y).

% Contrainte 1: un chiffre ne peut se trouver deux fois sur la même ligne
:- case(X, Y, Z), case(X, YY, Z), Y != YY, pos(X), pos(Y), pos(YY), valeur(Z).

% Contrainte 2: un chiffre ne peut se trouver deux fois sur la même colonne
:- case(X, Y, Z), case(XX, Y, Z), X != XX, pos(X), pos(XX), pos(Y), valeur(Z).

% Contrainte 3: un chiffre ne peut se trouver deux fois dans la même région
:- case(X, Y, Z), case(XX, YY, Z), X/3==XX/3, Y/3==YY/3, X != XX, Y != YY, pos(X), pos(Y), pos(XX), pos(YY), valeur(Z).


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% ---------------------- GRILLE DE L'ENONCE ---------------------- %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

case(0,1,7).
case(0,3,6).
case(0,5,3).
case(0,7,1).
case(0,8,4).

case(1,0,8).
case(1,4,9).

case(2,1,6).
case(2,2,3).
case(2,3,2).
case(2,4,1).
case(2,6,8).

case(3,4,3).
case(3,7,5).
case(3,8,9).

case(4,0,6).
case(4,3,9).
case(4,6,4).
case(4,8,1).

case(5,0,9).
case(5,1,2).
case(5,3,4).
case(5,5,1).
case(5,8,8).

case(6,1,5).
case(6,3,8).
case(6,8,3).

case(7,0,4).
case(7,2,1).
case(7,3,3).
case(7,4,6).
case(7,5,2).
case(7,6,7).
case(7,7,8).

case(8,0,3).
case(8,2,6).
case(8,3,1).
case(8,6,9).
case(8,8,2).


%%%%%%%%%%%%%%%%%%%%%% Résolution de la grille de Sudoku %%%%%%%%%%%%%%%%%%%%%%

% Il y a une unique solution:
% Answer: 1
% case(0,1,7) case(0,3,6) case(0,5,3) case(0,7,1) case(0,8,4) 
% case(1,0,8) case(1,4,9) 
% case(2,1,6) case(2,2,3) case(2,3,2) case(2,4,1) case(2,6,8) 
% case(3,4,3) case(3,7,5) case(3,8,9) 
% case(4,0,6) case(4,3,9) case(4,6,4) case(4,8,1) 
% case(5,0,9) case(5,1,2) case(5,3,4) case(5,5,1) case(5,8,8) 
% case(6,1,5) case(6,3,8) case(6,8,3) 
% case(7,0,4) case(7,2,1) case(7,3,3) case(7,4,6) case(7,5,2) case(7,6,7) case(7,7,8) 
% case(8,0,3) case(8,2,6) case(8,3,1) case(8,6,9) case(8,8,2) 
% valeur(1) valeur(2) valeur(3) valeur(4) valeur(5) valeur(6) valeur(7) valeur(8) valeur(9) 
% pos(0) pos(1) pos(2) pos(3) pos(4) pos(5) pos(6) pos(7) pos(8) 
% case(1,1,1) case(3,0,1) case(6,6,1) case(0,0,2) case(1,7,2) case(3,6,2) 
% case(4,4,2) case(6,2,2) case(1,6,3) case(4,1,3) case(5,7,3) case(1,2,4) 
% case(2,5,4) case(3,1,4) case(6,4,4) case(8,7,4) case(0,6,5) case(1,3,5) 
% case(2,0,5) case(4,2,5) case(5,4,5) case(7,8,5) case(8,5,5) case(1,8,6) 
% case(3,5,6) case(5,6,6) case(6,7,6) case(1,5,7) case(2,8,7) case(3,3,7) 
% case(4,7,7) case(5,2,7) case(6,0,7) case(8,4,7) case(0,4,8) case(3,2,8) 
% case(4,5,8) case(8,1,8) case(0,2,9) case(2,7,9) case(6,5,9) case(7,1,9)